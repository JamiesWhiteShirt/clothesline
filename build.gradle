buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.12-0.0.0.0"
group = "com.jamieswhiteshirt.clothesline" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "clothesline"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.0.2493"
    runDir = "run"

    clientJvmArgs = serverJvmArgs = ["-Dfml.coreMods.load=$fmlCorePlugin"]

    mappings = "snapshot_20170628"
}

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

configurations {
    contained
    contained.transitive = false
}

dependencies {
    contained "com.jamieswhiteshirt:rtree-3i-lite:0.1-SNAPSHOT"
    compile "com.jamieswhiteshirt:rtree-3i-lite:0.1-SNAPSHOT"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    core {
        compileClasspath = main.compileClasspath
        runtimeClasspath = main.runtimeClasspath
    }
    main {
        compileClasspath += core.output
        runtimeClasspath += core.output
    }
}

task coreJar(type: Jar) {
    from sourceSets.core.output

    manifest {
        attributes 'FMLCorePlugin': fmlCorePlugin, 'Maven-Version': "${project.group}:${project.archivesBaseName}:${project.version}:core"
    }

    classifier = 'core'
}

reobf {
    coreJar {
        mappingType = 'SEARGE'
    }
}

jar {
    from sourceSets.api.output
    from sourceSets.main.output

    from(coreJar.archivePath.absolutePath) {
        include '*'
    }

    from(configurations.contained.files) {
        include '*'
    }

    manifest {
        attributes 'ContainedDeps': ([coreJar.archivePath] + configurations.contained.files).collect { it.name }.join(" ")
    }

    dependsOn reobfCoreJar
}

task sourcesJar(type: Jar) {
    from sourceSets.api.allSource
    from sourceSets.main.allSource
    from sourceSets.core.allSource
    classifier = 'sources'
}

task apiJar(type: Jar) {
    from sourceSets.api.output
    classifier = 'api'
}
